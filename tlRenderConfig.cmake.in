@PACKAGE_INIT@

# \todo Is this the correct way to locate the custom Find*.cmake files?
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

include(CMakeFindDependencyMacro)

# Base dependencies
find_package(Imath REQUIRED)
find_package(nlohmann_json REQUIRED)
find_package(ZLIB REQUIRED)
find_package(minizip REQUIRED)
find_package(OpenTimelineIO REQUIRED)

# OpenColorIO dependencies
if(@TLRENDER_OCIO@)
    find_package(OpenColorIO REQUIRED)
    add_definitions(-DTLRENDER_OCIO)
endif()

# SDL dependencies
if(@TLRENDER_SDL2@)
    find_package(SDL2 REQUIRED)
    add_definitions(-DTLRENDER_SDL2)
endif()
if(@TLRENDER_SDL3@)
    find_package(SDL3 REQUIRED)
    add_definitions(-DTLRENDER_SDL3)
endif()

# I/O dependencies
if(@TLRENDER_JPEG@)
    find_package(libjpeg-turbo)
    if(libjpeg-turbo_FOUND)
        add_definitions(-DTLRENDER_JPEG)
    endif()
endif()
if(@TLRENDER_TIFF@)
    find_package(TIFF)
    if(TIFF_FOUND)
        add_definitions(-DTLRENDER_TIFF)
    endif()
endif()
if(@TLRENDER_STB@)
    add_definitions(-DTLRENDER_STB)
endif()
if(@TLRENDER_PNG@)
    find_package(PNG)
    if(PNG_FOUND)
        add_definitions(-DTLRENDER_PNG)
    endif()
endif()
if(@TLRENDER_EXR@)
    find_package(OpenEXR)
    if(OpenEXR_FOUND)
        add_definitions(-DTLRENDER_EXR)
    endif()
endif()
if(@TLRENDER_FFMPEG@)
    if(WIN32)
        set(FFmpeg_FOUND TRUE)
        add_library(FFmpeg::avdevice STATIC IMPORTED)
        set_property(TARGET FFmpeg::avdevice PROPERTY
             IMPORTED_LOCATION "${CMAKE_INSTALL_PREFIX}/lib/avdevice.lib")
        add_library(FFmpeg::avformat STATIC IMPORTED)
        set_property(TARGET FFmpeg::avformat PROPERTY
             IMPORTED_LOCATION "${CMAKE_INSTALL_PREFIX}/lib/avformat.lib")
        add_library(FFmpeg::avcodec STATIC IMPORTED)
        set_property(TARGET FFmpeg::avcodec PROPERTY
             IMPORTED_LOCATION "${CMAKE_INSTALL_PREFIX}/lib/avcodec.lib")
        add_library(FFmpeg::swresample STATIC IMPORTED)
        set_property(TARGET FFmpeg::swresample PROPERTY
             IMPORTED_LOCATION "${CMAKE_INSTALL_PREFIX}/lib/swresample.lib")
        add_library(FFmpeg::swscale STATIC IMPORTED)
        set_property(TARGET FFmpeg::swscale PROPERTY
             IMPORTED_LOCATION "${CMAKE_INSTALL_PREFIX}/lib/swscale.lib")
        add_library(FFmpeg::avutil STATIC IMPORTED)
        set_property(TARGET FFmpeg::avutil PROPERTY
             IMPORTED_LOCATION "${CMAKE_INSTALL_PREFIX}/lib/avutil.lib")
        add_library(FFmpeg::FFmpeg INTERFACE IMPORTED)
        set_property(TARGET FFmpeg::FFmpeg PROPERTY
            INTERFACE_LINK_LIBRARIES
            FFmpeg::avdevice
            FFmpeg::avformat
            FFmpeg::avcodec
            FFmpeg::swresample
            FFmpeg::swscale
            FFmpeg::avutil)
    else()
        find_package(PkgConfig REQUIRED)
        pkg_check_modules(
            swresample REQUIRED IMPORTED_TARGET
            libswresample)
        pkg_check_modules(
            FFmpeg REQUIRED IMPORTED_TARGET
            libavdevice
            libavformat
            libavcodec
            libswresample
            libswscale
            libavutil)
        add_library(FFmpeg::FFmpeg ALIAS PkgConfig::FFmpeg)
        add_library(FFmpeg::swresample ALIAS PkgConfig::swresample)
    endif()
    if(FFmpeg_FOUND)
        add_definitions(-DTLRENDER_FFMPEG)
    endif()
endif()
if(@TLRENDER_USD@)
    find_package(pxr)
    if(pxr_FOUND)
        add_definitions(-DTLRENDER_USD)
    endif()
endif()

# dtk dependencies
find_package(dtk REQUIRED)

# Qt dependencies
if(@TLRENDER_QT6@)
    set(CMAKE_AUTOMOC ON)
    set(CMAKE_AUTORCC ON)
    set(CMAKE_AUTOUIC ON)
    find_package(Qt6 REQUIRED COMPONENTS Quick Widgets OpenGLWidgets Svg HINTS "$ENV{QTDIR}")
elseif(@TLRENDER_QT5@)
    set(CMAKE_AUTOMOC ON)
    set(CMAKE_AUTORCC ON)
    set(CMAKE_AUTOUIC ON)
    find_package(Qt5 REQUIRED COMPONENTS Quick Widgets Svg HINTS "$ENV{QTDIR}")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/tlResourceTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/tlCoreTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/tlIOTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/tlTimelineTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/tlTimelineGLTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/tlDeviceTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/tlTimelineUITargets.cmake")
if(@TLRENDER_QT6@ OR @TLRENDER_QT5@)
    include("${CMAKE_CURRENT_LIST_DIR}/tlQtTargets.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/tlQtWidgetTargets.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/tlQtQuickTargets.cmake")
endif()

# \todo Is this the correct way to add the include directory?
include_directories("@PACKAGE_INCLUDE_INSTALL_DIR@")

check_required_components(tlRender)
