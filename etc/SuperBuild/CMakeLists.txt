cmake_minimum_required(VERSION 3.19)
cmake_policy(SET CMP0091 NEW)
cmake_policy(SET CMP0114 NEW)

project(tlRender-SuperBuild)

# Build options.
set(TLRENDER_ENABLE_MMAP TRUE CACHE BOOL "Enable memory-mapped file I/O")
set(TLRENDER_ENABLE_COVERAGE FALSE CACHE BOOL "Enable code coverage")
set(TLRENDER_ENABLE_PYTHON FALSE CACHE BOOL "Enable Python support (for OTIO Python adapters)")
set(TLRENDER_BUILD_GL TRUE CACHE BOOL "Build OpenGL support")
set(TLRENDER_BUILD_BMD FALSE CACHE BOOL "Build Blackmagic Design support")
set(TLRENDER_BMD_SDK "" CACHE PATH "Path to the Blackmagic Design SDK")
set(TLRENDER_BUILD_QT6 FALSE CACHE BOOL "Build Qt6 support")
set(TLRENDER_BUILD_QT5 FALSE CACHE BOOL "Build Qt5 support")
set(TLRENDER_BUILD_PROGRAMS TRUE CACHE BOOL "Build programs")
set(TLRENDER_BUILD_EXAMPLES TRUE CACHE BOOL "Build examples")
set(TLRENDER_BUILD_TESTS TRUE CACHE BOOL "Build tests")
if(WIN32)
    message(WARNING "See the README for how to build FFmpeg on Windows")
else()
    set(TLRENDER_BUILD_FFmpeg TRUE CACHE BOOL "Build FFmpeg support (Linux and macOS only)")
endif()
set(TLRENDER_BUILD_JPEG TRUE CACHE BOOL "Build JPEG support")
set(TLRENDER_BUILD_PNG TRUE CACHE BOOL "Build PNG support")
set(TLRENDER_BUILD_OpenEXR TRUE CACHE BOOL "Build OpenEXR support")
set(TLRENDER_BUILD_TIFF TRUE CACHE BOOL "Build TIFF support")

# Configure.
list(PREPEND CMAKE_MODULE_PATH
    ${PROJECT_SOURCE_DIR}/../../cmake/Modules
    ${PROJECT_SOURCE_DIR}/cmake/Modules)
set(CMAKE_CXX_STANDARD 14)
set(CMAKE_MSVC_RUNTIME_LIBRARY MultiThreaded$<$<CONFIG:Debug>:Debug>DLL)
set(CMAKE_FIND_FRAMEWORK LAST)

# https://stackoverflow.com/questions/45414507/pass-a-list-of-prefix-paths-to-externalproject-add-in-cmake-args
string(REPLACE ";" "|" CMAKE_PREFIX_PATH_TMP "${CMAKE_PREFIX_PATH}")

# Common arguments for building external projects.
set(TLRENDER_EXTERNAL_ARGS
    -DCMAKE_MODULE_PATH=${CMAKE_MODULE_PATH}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_PREFIX_PATH=${CMAKE_PREFIX_PATH_TMP}
    -DCMAKE_POLICY_DEFAULT_CMP0091:STRING=NEW
    -DCMAKE_POLICY_DEFAULT_CMP0114:STRING=NEW
    -DCMAKE_FIND_FRAMEWORK=${CMAKE_FIND_FRAMEWORK}
    -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
    -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
    -DCMAKE_MSVC_RUNTIME_LIBRARY=${CMAKE_MSVC_RUNTIME_LIBRARY}
    -DCMAKE_OSX_DEPLOYMENT_TARGET=${CMAKE_OSX_DEPLOYMENT_TARGET}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES}
    -DBUILD_SHARED_LIBS=${BUILD_SHARED_LIBS})

# External dependencies.
find_package(Threads REQUIRED)
if(TLRENDER_BUILD_GL)
	find_package(OpenGL REQUIRED)
endif()
list(APPEND TLRENDER_EXTERNAL_DEPS FSeq)
list(APPEND TLRENDER_EXTERNAL_DEPS GLM)
list(APPEND TLRENDER_EXTERNAL_DEPS ZLIB)
list(APPEND TLRENDER_EXTERNAL_DEPS Imath)
list(APPEND TLRENDER_EXTERNAL_DEPS nlohmann_json)
list(APPEND TLRENDER_EXTERNAL_DEPS FreeType)
list(APPEND TLRENDER_EXTERNAL_DEPS OCIO)
list(APPEND TLRENDER_EXTERNAL_DEPS OTIO)
list(APPEND TLRENDER_EXTERNAL_DEPS libsamplerate)
list(APPEND TLRENDER_EXTERNAL_DEPS RtAudio)
if((TLRENDER_BUILD_FFmpeg OR TLRENDER_BUILD_JPEG) AND NOT WIN32)
    list(APPEND TLRENDER_EXTERNAL_DEPS NASM)
endif()
if(TLRENDER_BUILD_FFmpeg)
    list(APPEND TLRENDER_EXTERNAL_DEPS FFmpeg)
endif()
if(TLRENDER_BUILD_JPEG)
    list(APPEND TLRENDER_EXTERNAL_DEPS JPEG)
endif()
if(TLRENDER_BUILD_OpenEXR)
    list(APPEND TLRENDER_EXTERNAL_DEPS OpenEXR)
endif()
if(TLRENDER_BUILD_PNG)
    list(APPEND TLRENDER_EXTERNAL_DEPS PNG)
endif()
if(TLRENDER_BUILD_TIFF)
    list(APPEND TLRENDER_EXTERNAL_DEPS TIFF)
endif()
if(TLRENDER_BUILD_GL)
    if(TLRENDER_BUILD_PROGRAMS OR TLRENDER_BUILD_EXAMPLES)
	list(APPEND TLRENDER_EXTERNAL_DEPS GLFW)
    endif()
endif()
foreach(EXTERNAL_DEP ${TLRENDER_EXTERNAL_DEPS})
    include(Build${EXTERNAL_DEP})
endforeach()

# Build tests.

if (TLRENDER_BUILD_TESTS)
  include(Buildtests)
endif()

# Build tlRender.
include(BuildtlRender)
